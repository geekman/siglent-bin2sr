// 
// Siglent binary format, 010 Editor template
//
// 2020.11.04  darell tan
//

LittleEndian();

local int i;

byte unknown[16];

for (i = 0; i < 4; i++) {
    double scale <comment="ch vertical scale (mV/div)">;
    FSkip(8);
}

for (i = 0; i < 4; i++) {
    double offset <comment="ch vertical offset (mV)">;
    FSkip(8);
}

FSeek(0xD4); double horizScaling  <comment="horizontal scale (seconds/div)">;
FSeek(0xE4); double triggerDelay <comment="trigger pos on screen (seconds)">;

FSeek(0xF4);
uint32 numPoints   <comment="number of points">;
double sampleRate  <comment="samples/sec">;

// no idea what these are, but they just look like similar blocks
struct {
    uint32 a;
    uint32 b;
    uint32 c;
    double d;
} s1, s2;

FSeek(0x800);
if (!FEof()) byte points1[numPoints]  <comment="ch 1 data points">;
if (!FEof()) byte points2[numPoints]  <comment="ch 2 data points">;
if (!FEof()) byte points3[numPoints]  <comment="ch 3 data points">;
if (!FEof()) byte points4[numPoints]  <comment="ch 4 data points">;
